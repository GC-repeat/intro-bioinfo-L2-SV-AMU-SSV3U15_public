################################################################
## Illustration of the different ways to display a protein
## 1. Cartoon
## 2. Atoms
## 3. Spheres
## 4. Electrostatic landscape
## 5. Hydrophobic landscape
## 6. Translucid surface with domain-soecific coloration
################################################################

close session


open 3nhn ; 
open 3nhn ; 
open 3nhn ; 
open 3nhn ; 
open 3nhn ; 
open 3nhn ; 
view orient ;
zoom 0.4 ; 

# ui mousemode right "move picked models" ; 

movie record # size 1984,1240 ;
wait 100 ;

## Move the models to avoid overlap
view matrix models #1,1,0,0,-60,0,1,0,+35,0,0,1,0 ; wait 20 ;
view matrix models #2,1,0,0,0,0,1,0,+35,0,0,1,0 ; wait 20 ;
view matrix models #3,1,0,0,+60,0,1,0,+35,0,0,1,0 ; wait 20 ;
view matrix models #4,1,0,0,-60,0,1,0,-35,0,0,1,0 ; wait 20 ;
view matrix models #5,1,0,0,0,0,1,0,-35,0,0,1,0 ; wait 20 ;
view matrix models #6,1,0,0,+60,0,1,0,-35,0,0,1,0 ; wait 20 ;
view orient ;
zoom 0.9 ; 


2dlabels text Rainbow color #1E90FF xpos 0.15 ypos 0.95 ;
select #1 ; rainbow sel ; select clear ; wait 50 ; 

2dlabels text Atoms color #1E90FF xpos 0.40 ypos 0.95 ;
select #2 ; hide sel cartoons ; show sel atoms ; select clear ; wait 50 ; 

2dlabels text Spheres color #1E90FF xpos 0.63 ypos 0.95 ;
select #3 ; hide sel cartoons ; show sel atoms ; style sel sphere ; select clear ; wait 50 ; 

2dlabels text Electrostatic color #1E90FF xpos 0.15 ypos 0.50 ;
select #4 ; coulombic sel ; select clear ; wait 50 ; 

2dlabels text Hydrophobicity color #1E90FF xpos 0.40 ypos 0.50 ;
select #5 ; mlp sel ; select clear ; wait 50 ; 

2dlabels text "Domain-colored surfaces" color #1E90FF xpos 0.63 ypos 0.50 ;
select #6 ; show sel surfaces ; transparency (#!6 & sel) 70 ; select clear ; wait 20 ; 

## Color the domains
sel #6:87-134 ; color (#!6 & sel) blue ;select clear ; wait 20 ; 
sel #6:148-260 ; color (#!6 & sel) orange ; select clear ; wait 20 ;

wait 200 ; 

cd "/Users/jvanheld/Desktop/structures/3nhn"

## Save movie
movie encode 3nhn_SRC-family-kinase-HCK-SH3-SH2-linker-regulatory-region.mov format mov

## Save image with white background for printable image
preset "overall look" "publication 1 (silhouettes)" ; 
save "3nhn_SRC-family-kinase-HCK-SH3-SH2-linker-regulatory-region_wbg.png" width 1984 height 1240 supersample 3

## Save image with black background for screen display
preset "overall look" interactive  ; 
save "3nhn_SRC-family-kinase-HCK-SH3-SH2-linker-regulatory-region_bbg.png" width 1984 height 1240 supersample 3

## Save session
save "3nhn_SRC-family-kinase-HCK-SH3-SH2-linker-regulatory-region.cxs"


